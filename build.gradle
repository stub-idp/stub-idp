buildscript {
    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos/' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'

archivesBaseName = 'stub-idp'

version = "${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
def buildNumber = "$version"

ext {
    opensaml_version = '3.3.0'
}

def dependencyVersions = [
        stub_idp_saml         : "$opensaml_version-68",
        ida_utils             : '2.0.0-332',
        dropwizard            : '1.1.4',
        ida_dev_pki           : '1.1.0-21',
        saml_libs             : "$opensaml_version-103",
        ida_test_utils        : '2.0.0-38',
        hub_saml              : "$opensaml_version-120",
        hk2_version           : '2.5.0-b05',
        dropwizard_infinispan : '1.1.4-42'
]

repositories {
    maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos/' }
}

dependencies {
    compile 'com.hubspot.dropwizard:dropwizard-guicier:1.0.0.6',
            // 0-jersey2-guice-spi has to be near the top of the dependencies
            'com.squarespace.jersey2-guice:0-jersey2-guice-spi:1.0.6',
            'de.neuland-bfi:jade4j:0.4.0',
            "io.dropwizard:dropwizard-assets:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-auth:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-client:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-configuration:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-metrics-graphite:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-views:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-views-freemarker:$dependencyVersions.dropwizard",
            "org.flywaydb:flyway-core:4.2.0",
            'org.freemarker:freemarker:2.3.27-incubating',
            "org.jdbi:jdbi3-core:3.0.0",
            'org.jsoup:jsoup:1.6.1',
            'org.mindrot:jbcrypt:0.3m',
            "org.opensaml:opensaml-core:$dependencyVersions.opensaml",
            "org.postgresql:postgresql:42.1.4",
            "uk.gov.ida:common-utils:$dependencyVersions.ida_utils",
            "uk.gov.ida:dropwizard-infinispan:$dependencyVersions.dropwizard_infinispan",
            "uk.gov.ida:saml-extensions:$dependencyVersions.saml_libs",
            "uk.gov.ida:rest-utils:$dependencyVersions.ida_utils",
            "uk.gov.ida:saml-metadata-bindings:$dependencyVersions.saml_libs",
            "uk.gov.ida:saml-security:$dependencyVersions.saml_libs",
            "uk.gov.ida:security-utils:$dependencyVersions.ida_utils",
            "uk.gov.ida:stub-idp-saml:$dependencyVersions.stub_idp_saml",
            "uk.gov.ida:saml-utils:$dependencyVersions.saml_libs"

    runtime 'joda-time:joda-time:2.3',
            'net.logstash.logback:logstash-logback-encoder:4.9'

    testCompile "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard",
            "uk.gov.ida:stub-idp-saml-test:$dependencyVersions.stub_idp_saml",
            "uk.gov.ida:ida-dev-pki:$dependencyVersions.ida_dev_pki",
            "uk.gov.ida:saml-metadata-bindings-test:$dependencyVersions.saml_libs",
            "uk.gov.ida:common-test-utils:$dependencyVersions.ida_test_utils",
            "uk.gov.ida:hub-saml:$dependencyVersions.hub_saml",
            "uk.gov.ida:hub-saml-test-utils:$dependencyVersions.hub_saml",
            "com.h2database:h2:1.4.196"

}

jar {
    archiveName = "stub-idp.jar"
    destinationDir = file(new File("$buildDir/output"))
    manifest {
        attributes(
                "Main-Class": 'uk.gov.ida.stub.idp.StubIdpApplication',
                "Class-Path": configurations.compile.collect { "lib/"+it.getName() }.join(' ')
        )
    }
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'configuration'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

task integrationTest(type: Test) {
    setTestClassesDirs sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
}

task copyToLib(dependsOn: jar, type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

task zip(dependsOn: copyToLib, type: Zip) {
    from "$buildDir/output"
}

task(outputDependencies) {
    doLast {
        println "saml_lib=" + dependencyVersions.saml_lib
        println "ida_utils=" + dependencyVersions.ida_utils
    }
}

jar.doFirst {
    mkdir("build/output")
}
